ServerModule:
  Name:               A28AssetsServer
  Target Platform:    Windows/IBM i/Linux

  Multilingual Details:
  - ENG:
      Description:        "Assets Server"

  - FRA:
      Description:        "Assets Server"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_SRVM) Sessionidentifier('A28SESSION')
    
    Def_List Name(#List) Fields(#PropertyID #PropertyName #PropertyAddress #PropertyCity) Type(*WORKING) Entrys(*MAX)
    
    Group_By Name(#Fields) Fields(#PropertyID #PropertyName #PropertyAddress #PropertyCity #PropertyState #PropertyZip #PropertyManager #PropertyYearBuilt #PropertyTenant #PropertyExpiration #PropertyType #PropertyOccupancy #PropertyTotalSF #PropertyOfficeSF #PropertyOfficePercent #PropertyIndustrialSF #PropertyIndustrialPercent #PropertyAcres #PropertyFAR #PropertyType2 #PropertyType3 #PropertyType4 #PropertyFloors #PropertyTypeMaterial)
    
    Srvroutine Name(Find) Session(*REQUIRED)
    Field_Map For(*INPUT) Field(#STD_STRNG) Parameter_Name(Filter)
    List_Map For(*OUTPUT) List(#List)
    
    #STD_STRNG := #STD_STRNG.UpperCase.Trim
    
    Select Fields(#List) From_File(GCAssets) Io_Error(*NEXT) Val_Error(*NEXT)
    
    If ((#STD_STRNG = *BLANKS) *OrIf (#PropertyName.UpperCase.Contains( #STD_STRNG )) *OrIf (#PropertyAddress.UpperCase.Contains( #STD_STRNG )) *OrIf (#PropertyCity.UpperCase.Contains( #STD_STRNG )))
    
    Add_Entry To_List(#List)
    
    Endif
    
    Endselect
    
    Endroutine
    
    Srvroutine Name(Read) Session(*REQUIRED)
    Field_Map For(*INPUT) Field(#PropertyID)
    Group_Map For(*OUTPUT) Group(#Fields)
    
    Fetch Fields(#Fields) From_File(GCAssets) With_Key(#PropertyID) Io_Error(*NEXT) Val_Error(*NEXT)
    
    Endroutine
    
    Srvroutine Name(Save) Session(*REQUIRED)
    Group_Map For(*INPUT) Group(#Fields)
    Field_Map For(*OUTPUT) Field(#STD_CODE) Parameter_Name(ReturnCode)
    
    Update Fields(#Fields) In_File(GCAssets) With_Key(#PropertyID) Io_Error(*NEXT) Val_Error(*NEXT)
    
    If_Status Is(*NORECORD)
    
    Insert Fields(#Fields) To_File(GCAssets) Io_Error(*NEXT) Val_Error(*NEXT)
    
    Endif
    
    If_Status Is(*OKAY)
    
    #STD_CODE := "OK"
    
    Else
    
    #STD_CODE := "ER"
    
    Endif
    
    Endroutine
    
    Srvroutine Name(Delete) Session(*REQUIRED)
    Field_Map For(*INPUT) Field(#PropertyID)
    
    Delete From_File(GCAssets) With_Key(#PropertyID) Io_Error(*NEXT) Val_Error(*NEXT)
    
    Endroutine
    
    End_Com
