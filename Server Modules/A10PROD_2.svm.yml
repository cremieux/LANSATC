ServerModule:
  Name:               A10ProductsServer
  Target Platform:    Windows/IBM i/Linux

  Multilingual Details:
  - ENG:
      Description:        "Products Server"

  - FRA:
      Description:        "Products Server"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_SRVM) Sessionidentifier('A10SESSION')
    
    Def_List Name(#List) Fields(#xProductID #xProductDescription #xProductPrice #xProductDetail) Type(*WORKING) Entrys(*MAX)
    
    Group_By Name(#Fields) Fields(#xProductID #xProductCategoryID #xProductLineID #xProductDescription #xProductModel #xProductPrice #xProductDetail #xProductShippingCost #xProductStockStatus #xProductImage #xProductTaxPercentage)
    
    Srvroutine Name(Find) Session(*REQUIRED)
    Field_Map For(*INPUT) Field(#STD_STRNG) Parameter_Name(Filter)
    List_Map For(*OUTPUT) List(#List)
    
    #STD_STRNG := #STD_STRNG.UpperCase.Trim
    
    Select Fields(#List) From_File(xProduct) Io_Error(*NEXT) Val_Error(*NEXT)
    
    If ((#STD_STRNG = *BLANKS) *OrIf (#xProductDescription.UpperCase.Contains( #STD_STRNG )) *OrIf (#xProductPrice.AsString.UpperCase.Contains( #STD_STRNG )) *OrIf (#xProductDetail.UpperCase.Contains( #STD_STRNG )))
    
    Add_Entry To_List(#List)
    
    Endif
    
    Endselect
    
    Endroutine
    
    Srvroutine Name(Read) Session(*REQUIRED)
    Field_Map For(*INPUT) Field(#xProductID)
    Group_Map For(*OUTPUT) Group(#Fields)
    
    Fetch Fields(#Fields) From_File(xProduct) With_Key(#xProductID) Io_Error(*NEXT) Val_Error(*NEXT)
    
    Endroutine
    
    Srvroutine Name(Save) Session(*REQUIRED)
    Group_Map For(*INPUT) Group(#Fields)
    Field_Map For(*OUTPUT) Field(#STD_CODE) Parameter_Name(ReturnCode)
    
    Update Fields(#Fields) In_File(xProduct) With_Key(#xProductID) Io_Error(*NEXT) Val_Error(*NEXT)
    
    If_Status Is(*NORECORD)
    
    Insert Fields(#Fields) To_File(xProduct) Io_Error(*NEXT) Val_Error(*NEXT)
    
    Endif
    
    If_Status Is(*OKAY)
    
    #STD_CODE := "OK"
    
    Else
    
    #STD_CODE := "ER"
    
    Endif
    
    Endroutine
    
    Srvroutine Name(Delete) Session(*REQUIRED)
    Field_Map For(*INPUT) Field(#xProductID)
    
    Delete From_File(xProduct) With_Key(#xProductID) Io_Error(*NEXT) Val_Error(*NEXT)
    
    Endroutine
    
    End_Com
